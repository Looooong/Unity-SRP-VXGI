#pragma kernel CSAggregate D3D11
#pragma kernel CSAggregate
#pragma kernel CSClear
#pragma kernel CSRender

#include "Packages/com.looooong.srp.vxgi/ShaderLibrary/Structs/VoxelData.cginc"
#include "Packages/com.looooong.srp.vxgi/ShaderLibrary/Structs/VoxelLightingData.hlsl"
#include "Packages/com.looooong.srp.vxgi/ShaderLibrary/Radiances/Voxel.cginc"

#define RADIANCE_PRECISION 256.0

RWTexture3D<uint> RadianceRG;
RWTexture3D<uint> RadianceBA;
RWTexture3D<uint> RadianceCount;
RWTexture3D<float4> Target;
ConsumeStructuredBuffer<VoxelData> VoxelBuffer;

[numthreads(4,4,4)]
void CSAggregate(uint3 id : SV_DispatchThreadID)
{
  if (any(id >= Resolution)) return;

  // FIXME
  // It looks stupid, due to a bug in HLSL to GLSL translator (case 1241093).
  // This is an only workaround I can think of.
  uint rg = RadianceRG[id];
  uint ba = RadianceBA.Load(id);
  uint count = RadianceCount[id];

  if (count > 0u) {
    float4 color = uint4(
      rg & 0xffff,
      rg >> 16u,
      ba & 0xffff,
      ba >> 16u
    ) / RADIANCE_PRECISION;

    Target[id] = color / count;
  } else {
#ifdef D3D11
    // Write only when necessary
    if (any(Target[id])) Target[id] = 0.0;
#else
    Target[id] = 0.0;
#endif
  }
}

[numthreads(4,4,4)]
void CSClear(uint3 id : SV_DispatchThreadID)
{
  if (any(id >= Resolution)) return;

  RadianceRG[id] = 0u;
  RadianceBA[id] = 0u;
  RadianceCount[id] = 0u;
}

[numthreads(64,1,1)]
void CSRender(uint id : SV_DispatchThreadID)
{
  VoxelData data = VoxelBuffer.Consume();

  DeviceMemoryBarrierWithGroupSync();

  if (!data.IsValid()) return;

  float3 position = data.GetPosition();
  float3 normal = data.GetNormal();
  float4 color = data.GetColor();
  float3 emission = data.GetEmission();

  VoxelLightingData lightingData;
  lightingData.color = color.rgb;
  lightingData.voxelPosition = position;
  lightingData.vecN = normal;
  lightingData.Initialize();

  color.rgb = emission + VoxelRadiance(lightingData);
  color.a = saturate(color.a);

  uint4 intColor = (uint4)round(color * RADIANCE_PRECISION);

  InterlockedAdd(RadianceRG[position], intColor.r | intColor.g << 16);
  InterlockedAdd(RadianceBA[position], intColor.b | intColor.a << 16);
  InterlockedAdd(RadianceCount[position], 1);
}
